group 'com.amazonaws.amplify.amplify_api'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.2.1'
    }
}

rootProject.allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: './coverage.gradle'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

// Logic for finding sibling plugins, from `flutter_tools/gradle/flutter.gradle`
def amplifyFlutter = project.rootProject.findProject(':amplify_flutter_android')

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    defaultConfig {
        minSdkVersion 24
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'AndroidGradlePluginVersion'
        informational 'GradleDependency'

        abortOnError true
        warningsAsErrors true
        checkTestSources true
        xmlReport false
        htmlReport false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
}

dependencies {
    api amplifyFlutter

    implementation "com.amplifyframework:aws-api:1.36.6-dev-preview.0"
    implementation "com.amplifyframework:aws-api-appsync:1.36.6-dev-preview.0"

    // Do not update before migration of plugin to Dart.
    //noinspection GradleDependency
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.mockito:mockito-inline:3.11.2'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    // Tests must be updated if bumped
    //noinspection GradleDependency
    testImplementation 'org.robolectric:robolectric:4.3.1'

    // Do not update before migration of plugin to Dart.
    //noinspection GradleDependency
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9'
}
