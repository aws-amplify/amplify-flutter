group 'com.amazonaws.amplify.amplify_auth_cognito'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.2.1'
    }
}

rootProject.allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: './coverage.gradle'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

// Logic for finding sibling plugins, from `flutter_tools/gradle/flutter.gradle`
def amplifyFlutter = project.rootProject.findProject(':amplify_flutter_android')

android {
    compileSdkVersion 31

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.resources.srcDirs += '../../amplify_auth_cognito/test/resources'
    }
    defaultConfig {
        minSdkVersion 24
    }
    lintOptions {
        disable 'AndroidGradlePluginVersion'
        informational 'GradleDependency'

        abortOnError true
        warningsAsErrors true
        checkTestSources true
        xmlReport false
        htmlReport false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
}

dependencies {
    api amplifyFlutter

    implementation 'com.amplifyframework:aws-auth-cognito:1.36.5-dev-preview.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    implementation 'androidx.browser:browser:1.4.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.5.1'
}
