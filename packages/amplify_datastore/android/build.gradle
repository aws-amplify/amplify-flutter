group 'com.amazonaws.amplify.amplify_datastore'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '2.2.0'
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:13.1.0'
    }
}

rootProject.allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdk 36

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.resources.srcDirs += '../test/resources'
    }
    defaultConfig {
        minSdkVersion 24
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'AndroidGradlePluginVersion'
        informational 'GradleDependency'

        abortOnError true
        warningsAsErrors true
        checkTestSources true
        xmlReport false
        htmlReport false
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    kotlinOptions {
        jvmTarget = '17'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    namespace 'com.amazonaws.amplify.amplify_datastore'
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.1.5"

    implementation 'com.amplifyframework:aws-auth-cognito:2.33.0'
    implementation "com.amplifyframework:aws-api:2.33.0"
    implementation "com.amplifyframework:aws-datastore:2.33.0"
    implementation "com.amplifyframework:aws-api-appsync:2.33.0"
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.19.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'com.google.code.gson:gson:2.13.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'

    // Tests must be updated if bumped
    //noinspection GradleDependency
    testImplementation 'org.robolectric:robolectric:4.15.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.21'
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.21'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.21'
}
