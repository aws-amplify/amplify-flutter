group 'com.amazonaws.amplify.amplify_datastore'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.2.1'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion 31

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.resources.srcDirs += '../test/resources'
    }
    defaultConfig {
        minSdkVersion 21
    }
    lintOptions {
        disable 'InvalidPackage'
        disable 'AndroidGradlePluginVersion'
        informational 'GradleDependency'

        abortOnError true
        warningsAsErrors true
        checkTestSources true
        xmlReport false
        htmlReport false
    }
    compileOptions {
        // Support for Java 8 features
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    // Support for Java 8 features
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "com.amplifyframework:aws-datastore:1.37.12"
    implementation "com.amplifyframework:aws-api-appsync:1.37.12"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.10.0'
    testImplementation 'org.mockito:mockito-inline:3.10.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.6.3'
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.6.3'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
}
